import Mathlib.Data.Set.Basic
import Mathlib.Data.Set.Lattice

import Bookshelf.Enderton.Set.Chapter_1
import Common.Set.Basic

/-! # Enderton.Chapter_2

Axioms and Operations
-/

namespace Enderton.Set.Chapter_2


/-- ### Exercise 3.1

Assume that `A` is the set of integers divisible by `4`. Similarly assume that
`B` and `C` are the sets of integers divisible by `9` and `10`, respectively.
What is in `A âˆ© B âˆ© C`?
-/
theorem exercise_3_1 {A B C : Set â„¤}
  (hA : A = { x | Dvd.dvd 4 x })
  (hB : B = { x | Dvd.dvd 9 x })
  (hC : C = { x | Dvd.dvd 10 x })
  : âˆ€ x âˆˆ (A âˆ© B âˆ© C), (4 âˆ£ x) âˆ§ (9 âˆ£ x) âˆ§ (10 âˆ£ x) := by
  intro x h
  rw [Set.mem_inter_iff] at h
  have âŸ¨âŸ¨ha, hbâŸ©, hcâŸ© := h
  refine âŸ¨?_, âŸ¨?_, ?_âŸ©âŸ©
  Â· rw [hA] at ha
    exact Set.mem_setOf.mp ha
  Â· rw [hB] at hb
    exact Set.mem_setOf.mp hb
  Â· rw [hC] at hc
    exact Set.mem_setOf.mp hc

/-- ### Exercise 3.2

Give an example of sets `A` and `B` for which `â‹ƒ A = â‹ƒ B` but `A â‰  B`.
-/
theorem exercise_3_2 {A B : Set (Set â„•)}
  (hA : A = {{1}, {2}}) (hB : B = {{1, 2}})
  : Set.sUnion A = Set.sUnion B âˆ§ A â‰  B := by
  apply And.intro
  Â· show â‹ƒâ‚€ A = â‹ƒâ‚€ B
    ext x
    show (âˆƒ t, t âˆˆ A âˆ§ x âˆˆ t) â†” âˆƒ t, t âˆˆ B âˆ§ x âˆˆ t
    apply Iff.intro
    Â· intro âŸ¨t, âŸ¨ht, hxâŸ©âŸ©
      rw [hA] at ht
      refine âŸ¨{1, 2}, âŸ¨by rw [hB]; simp, ?_âŸ©âŸ©
      apply Or.elim ht <;>
      Â· intro ht'
        rw [ht'] at hx
        rw [hx]
        simp
    Â· intro âŸ¨t, âŸ¨ht, hxâŸ©âŸ©
      rw [hB] at ht
      rw [ht] at hx
      apply Or.elim hx
      Â· intro hx'
        exact âŸ¨{1}, âŸ¨by rw [hA]; simp, by rw [hx']; simpâŸ©âŸ©
      Â· intro hx'
        exact âŸ¨{2}, âŸ¨by rw [hA]; simp, by rw [hx']; simpâŸ©âŸ©
  Â· show A â‰  B
    -- Find an element that exists in `B` but not in `A`. Extensionality
    -- concludes the proof.
    intro h
    rw [hA, hB, Set.ext_iff] at h
    have hâ‚ := h {1, 2}
    simp at hâ‚
    rw [Set.ext_iff] at hâ‚
    have hâ‚‚ := hâ‚ 2
    simp at hâ‚‚

/-- ### Exercise 3.3

Show that every member of a set `A` is a subset of `U A`. (This was stated as an
example in this section.)
-/
theorem exercise_3_3 {A : Set (Set Î±)}
  : âˆ€ x âˆˆ A, x âŠ† Set.sUnion A := by
  intro x hx
  show âˆ€ y âˆˆ x, y âˆˆ { a | âˆƒ t, t âˆˆ A âˆ§ a âˆˆ t }
  intro y hy
  rw [Set.mem_setOf_eq]
  exact âŸ¨x, âŸ¨hx, hyâŸ©âŸ©

/-- ### Exercise 3.4

Show that if `A âŠ† B`, then `â‹ƒ A âŠ† â‹ƒ B`.
-/
theorem exercise_3_4 (h : A âŠ† B) : â‹ƒâ‚€ A âŠ† â‹ƒâ‚€ B := by
  show âˆ€ x âˆˆ { a | âˆƒ t, t âˆˆ A âˆ§ a âˆˆ t }, x âˆˆ { a | âˆƒ t, t âˆˆ B âˆ§ a âˆˆ t }
  intro x hx
  rw [Set.mem_setOf_eq] at hx
  have âŸ¨t, âŸ¨ht, hxtâŸ©âŸ© := hx
  rw [Set.mem_setOf_eq]
  exact âŸ¨t, âŸ¨h ht, hxtâŸ©âŸ©

/-- ### Exercise 3.5

Assume that every member of `ğ“` is a subset of `B`. Show that `â‹ƒ ğ“ âŠ† B`.
-/
theorem exercise_3_5 (h : âˆ€ x âˆˆ ğ“, x âŠ† B) : â‹ƒâ‚€ ğ“ âŠ† B := by
  unfold Set.sUnion sSup Set.instSupSetSet
  simp only
  show âˆ€ y âˆˆ { a | âˆƒ t, t âˆˆ ğ“ âˆ§ a âˆˆ t }, y âˆˆ B
  intro y hy
  rw [Set.mem_setOf_eq] at hy
  have âŸ¨t, âŸ¨htğ“, hytâŸ©âŸ© := hy
  exact (h t htğ“) hyt

/-- ### Exercise 3.6a

Show that for any set `A`, `â‹ƒ ğ“Ÿ A = A`.
-/
theorem exercise_3_6a : â‹ƒâ‚€ (Set.powerset A) = A := by
  unfold Set.sUnion sSup Set.instSupSetSet Set.powerset
  simp only
  ext x
  apply Iff.intro
  Â· intro hx
    rw [Set.mem_setOf_eq] at hx
    have âŸ¨t, âŸ¨htl, htrâŸ©âŸ© := hx
    rw [Set.mem_setOf_eq] at htl
    exact htl htr
  Â· intro hx
    rw [Set.mem_setOf_eq]
    exact âŸ¨A, âŸ¨by rw [Set.mem_setOf_eq], hxâŸ©âŸ©

/-- ### Exercise 3.6b

Show that `A âŠ† ğ“Ÿ â‹ƒ A`. Under what conditions does equality hold?
-/
theorem exercise_3_6b
  : A âŠ† Set.powerset (â‹ƒâ‚€ A)
  âˆ§ (A = Set.powerset (â‹ƒâ‚€ A) â†” âˆƒ B, A = Set.powerset B) := by
  apply And.intro
  Â· unfold Set.powerset
    show âˆ€ x âˆˆ A, x âˆˆ { t | t âŠ† â‹ƒâ‚€ A }
    intro x hx
    rw [Set.mem_setOf]
    exact exercise_3_3 x hx
  Â· apply Iff.intro
    Â· intro hA
      exact âŸ¨â‹ƒâ‚€ A, hAâŸ©
    Â· intro âŸ¨B, hBâŸ©
      conv => rhs; rw [hB, exercise_3_6a]
      exact hB

/-- ### Exercise 3.7a

Show that for any sets `A` and `B`, `ğ“Ÿ A âˆ© ğ“Ÿ B = ğ“Ÿ (A âˆ© B)`.
-/
theorem exercise_3_7A
  : Set.powerset A âˆ© Set.powerset B = Set.powerset (A âˆ© B) := by
  suffices
    Set.powerset A âˆ© Set.powerset B âŠ† Set.powerset (A âˆ© B) âˆ§
    Set.powerset (A âˆ© B) âŠ† Set.powerset A âˆ© Set.powerset B from
    subset_antisymm this.left this.right
  apply And.intro
  Â· unfold Set.powerset
    intro x hx
    simp only [Set.mem_inter_iff, Set.mem_setOf_eq] at hx
    rwa [Set.mem_setOf, Set.subset_inter_iff]
  Â· unfold Set.powerset
    simp
    intro x hA _
    exact hA

-- theorem false_of_false_iff_true : (false â†” true) â†’ false := by simp

/-- ### Exercise 3.7b (i)

Show that `ğ“Ÿ A âˆª ğ“Ÿ B âŠ† ğ“Ÿ (A âˆª B)`.
-/
theorem exercise_3_7b_i
  : Set.powerset A âˆª Set.powerset B âŠ† Set.powerset (A âˆª B) := by
  unfold Set.powerset
  intro x hx
  simp at hx
  apply Or.elim hx
  Â· intro hA
    rw [Set.mem_setOf_eq]
    exact Set.subset_union_of_subset_left hA B
  Â· intro hB
    rw [Set.mem_setOf_eq]
    exact Set.subset_union_of_subset_right hB A

/-- ### Exercise 3.7b (ii)

Under what conditions does `ğ“Ÿ A âˆª ğ“Ÿ B = ğ“Ÿ (A âˆª B)`.?
-/
theorem exercise_3_7b_ii
  : Set.powerset A âˆª Set.powerset B = Set.powerset (A âˆª B) â†” A âŠ† B âˆ¨ B âŠ† A := by
  unfold Set.powerset
  apply Iff.intro
  Â· intro h
    by_contra nh
    rw [not_or] at nh
    have âŸ¨a, hAâŸ© := Set.not_subset.mp nh.left
    have âŸ¨b, hBâŸ© := Set.not_subset.mp nh.right
    rw [Set.ext_iff] at h
    have hz := h {a, b}
    -- `hz` states that `{a, b} âŠ† A âˆ¨ {a, b} âŠ† B â†” {a, b} âŠ† A âˆª B`. We show the
    -- left-hand side is false but the right-hand side is true, yielding our
    -- contradiction.
    suffices Â¬({a, b} âŠ† A âˆ¨ {a, b} âŠ† B) by
      have hzâ‚ : a âˆˆ A âˆª B := by
        rw [Set.mem_union]
        exact Or.inl hA.left
      have hzâ‚‚ : b âˆˆ A âˆª B := by
        rw [Set.mem_union]
        exact Or.inr hB.left
      exact absurd (hz.mpr $ Set.mem_mem_imp_pair_subset hzâ‚ hzâ‚‚) this
    intro hAB
    exact Or.elim hAB
      (fun y => absurd (y $ show b âˆˆ {a, b} by simp) hB.right)
      (fun y => absurd (y $ show a âˆˆ {a, b} by simp) hA.right)
  Â· intro h
    ext x
    apply Or.elim h
    Â· intro hA
      apply Iff.intro
      Â· intro hx
        exact Or.elim hx
          (Set.subset_union_of_subset_left Â· B)
          (Set.subset_union_of_subset_right Â· A)
      Â· intro hx
        refine Or.inr (Set.Subset.trans hx ?_)
        exact subset_of_eq (Set.left_subset_union_eq_self hA)
    Â· intro hB
      apply Iff.intro
      Â· intro hx
        exact Or.elim hx
          (Set.subset_union_of_subset_left Â· B)
          (Set.subset_union_of_subset_right Â· A)
      Â· intro hx
        refine Or.inl (Set.Subset.trans hx ?_)
        exact subset_of_eq (Set.right_subset_union_eq_self hB)

/-- ### Exercise 3.9

Give an example of sets `a` and `B` for which `a âˆˆ B` but `ğ“Ÿ a âˆ‰ ğ“Ÿ B`.
-/
theorem exercise_3_9 (ha : a = {1}) (hB : B = {{1}})
  : a âˆˆ B âˆ§ Set.powerset a âˆ‰ Set.powerset B := by
  apply And.intro
  Â· rw [ha, hB]
    simp
  Â· intro h
    have hâ‚ : Set.powerset a = {âˆ…, {1}} := by
      rw [ha]
      exact Set.powerset_singleton 1
    have hâ‚‚ : Set.powerset B = {âˆ…, {{1}}} := by
      rw [hB]
      exact Set.powerset_singleton {1}
    rw [hâ‚, hâ‚‚] at h
    simp at h
    apply Or.elim h
    Â· intro h
      rw [Set.ext_iff] at h
      have := h âˆ…
      simp at this
    Â· intro h
      rw [Set.ext_iff] at h
      have := h 1
      simp at this

/-- ### Exercise 3.10

Show that if `a âˆˆ B`, then `ğ“Ÿ a âˆˆ ğ“Ÿ ğ“Ÿ â‹ƒ B`.
-/
theorem exercise_3_10 (ha : a âˆˆ B)
  : Set.powerset a âˆˆ Set.powerset (Set.powerset (â‹ƒâ‚€ B)) := by
  have hâ‚ := exercise_3_3 a ha
  have hâ‚‚ := Chapter_1.exercise_1_3 hâ‚
  generalize hb : ğ’« (â‹ƒâ‚€ B) = b
  conv => rhs; unfold Set.powerset
  rw [â† hb, Set.mem_setOf_eq]
  exact hâ‚‚

end Enderton.Set.Chapter_2