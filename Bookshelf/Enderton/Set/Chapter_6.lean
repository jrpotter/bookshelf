import Bookshelf.Enderton.Set.Chapter_4
import Common.Logic.Basic
import Common.Nat.Basic
import Common.Set.Basic
import Common.Set.Equinumerous
import Common.Set.Intervals
import Mathlib.Data.Finset.Card
import Mathlib.Data.Set.Finite
import Mathlib.Tactic.LibrarySearch

/-! # Enderton.Set.Chapter_6

Cardinal Numbers and the Axiom of Choice

NOTE: We choose to use injectivity/surjectivity concepts found in
`Mathlib.Data.Set.Function` over those in `Mathlib.Init.Function` since the
former provides noncomputable utilities around obtaining inverse functions
(namely `Function.invFunOn`).
-/

namespace Enderton.Set.Chapter_6

/-- #### Theorem 6B

No set is equinumerous to its powerset.
-/
theorem theorem_6b (A : Set Œ±)
  : A ‚ââ ùí´ A := by
  rw [Set.not_equinumerous_def]
  intro f hf
  unfold Set.BijOn at hf
  let œÜ := { a ‚àà A | a ‚àâ f a }
  suffices ‚àÄ a ‚àà A, f a ‚â† œÜ by
    have hœÜ := hf.right.right (show œÜ ‚àà ùí´ A by simp)
    have ‚ü®a, ha‚ü© := hœÜ
    exact absurd ha.right (this a ha.left)
  intro a ha hfa
  by_cases h : a ‚àà f a
  ¬∑ have h' := h
    rw [hfa] at h
    simp only [Set.mem_setOf_eq] at h
    exact absurd h' h.right
  ¬∑ rw [Set.Subset.antisymm_iff] at hfa
    have := hfa.right ‚ü®ha, h‚ü©
    exact absurd this h

/-! ### Pigeonhole Principle -/

/--
A subset of a finite set of natural numbers has a max member.
-/
lemma subset_finite_max_nat {S' S : Set ‚Ñï}
  (hS : Set.Finite S) (hS' : Set.Nonempty S') (h : S' ‚äÜ S)
  : ‚àÉ m, m ‚àà S' ‚àß ‚àÄ n, n ‚àà S' ‚Üí n ‚â§ m := by
  have ‚ü®m, hm‚ÇÅ, hm‚ÇÇ‚ü© :=
    Set.Finite.exists_maximal_wrt id S' (Set.Finite.subset hS h) hS'
  simp only [id_eq] at hm‚ÇÇ
  refine ‚ü®m, hm‚ÇÅ, ?_‚ü©
  intro n hn
  match @trichotomous ‚Ñï LT.lt _ m n with
  | Or.inr (Or.inl r) => exact Nat.le_of_eq r.symm
  | Or.inl r =>
    have := hm‚ÇÇ n hn (Nat.le_of_lt r)
    exact Nat.le_of_eq this.symm
  | Or.inr (Or.inr r) => exact Nat.le_of_lt r

/--
Auxiliary function to be proven by induction.
-/
lemma pigeonhole_principle_aux (n : ‚Ñï)
  : ‚àÄ M, M ‚äÇ Set.Iio n ‚Üí
      ‚àÄ f : ‚Ñï ‚Üí ‚Ñï,
        Set.MapsTo f M (Set.Iio n) ‚àß Set.InjOn f M ‚Üí
        ¬¨ Set.SurjOn f M (Set.Iio n) := by
  induction n with
  | zero =>
    intro _ hM
    unfold Set.Iio at hM
    simp only [Nat.zero_eq, not_lt_zero', Set.setOf_false] at hM
    rw [Set.ssubset_empty_iff_false] at hM
    exact False.elim hM
  | succ n ih =>
    intro M hM f ‚ü®hf_maps, hf_inj‚ü© hf_surj

    by_cases hM' : M = ‚àÖ
    ¬∑ unfold Set.SurjOn at hf_surj
      rw [hM'] at hf_surj
      simp only [Set.image_empty] at hf_surj
      rw [Set.subset_def] at hf_surj
      exact hf_surj n (show n < n + 1 by simp)

    by_cases h : ¬¨ ‚àÉ t, t ‚àà M ‚àß f t = n
    -- Trivial case. `f` must not be onto if this is the case.
    ¬∑ have ‚ü®t, ht‚ü© := hf_surj (show n ‚àà _ by simp)
      exact absurd ‚ü®t, ht‚ü© h

    -- Continue under the assumption `n ‚àà ran f`.
    simp only [not_not] at h
    have ‚ü®t, ht‚ÇÅ, ht‚ÇÇ‚ü© := h

    -- `M ‚â† ‚àÖ` so `‚àÉ p, ‚àÄ x ‚àà M, p ‚â• x`.
    have ‚ü®p, hp‚ÇÅ, hp‚ÇÇ‚ü© : ‚àÉ p ‚àà M, ‚àÄ x, x ‚àà M ‚Üí p ‚â• x := by
      refine subset_finite_max_nat (show Set.Finite M from ?_) ?_ ?_
      ¬∑ have := Set.finite_lt_nat (n + 1)
        exact Set.Finite.subset this (subset_of_ssubset hM)
      ¬∑ exact Set.nmem_singleton_empty.mp hM'
      ¬∑ show ‚àÄ t, t ‚àà M ‚Üí t ‚àà M
        simp only [imp_self, forall_const]

    -- `g` is a variant of `f` in which the largest element of its domain
    -- (i.e. `p`) corresponds to value `n`.
    let g x := if x = p then n else if x = t then f p else f x

    have hg_maps : Set.MapsTo g M (Set.Iio (n + 1)) := by
      intro x hx
      dsimp only
      by_cases hx‚ÇÅ : x = p
      ¬∑ rw [hx‚ÇÅ]
        simp
      ¬∑ rw [if_neg hx‚ÇÅ]
        by_cases hx‚ÇÇ : x = t
        ¬∑ rw [hx‚ÇÇ]
          simp only [ite_true, Set.mem_Iio]
          exact hf_maps hp‚ÇÅ
        ¬∑ rw [if_neg hx‚ÇÇ]
          simp only [Set.mem_Iio]
          exact hf_maps hx

    have hg_inj : Set.InjOn g M := by
      intro x‚ÇÅ hx‚ÇÅ x‚ÇÇ hx‚ÇÇ hf'
      by_cases hc‚ÇÅ : x‚ÇÅ = p
      ¬∑ by_cases hc‚ÇÇ : x‚ÇÇ = p
        ¬∑ rw [hc‚ÇÅ, hc‚ÇÇ]
        ¬∑ dsimp at hf'
          rw [hc‚ÇÅ] at hf'
          simp only [ite_self, ite_true] at hf'
          by_cases hc‚ÇÉ : x‚ÇÇ = t
          ¬∑ rw [if_neg hc‚ÇÇ, if_pos hc‚ÇÉ, ‚Üê ht‚ÇÇ] at hf'
            rw [hc‚ÇÅ] at hx‚ÇÅ ‚ä¢
            rw [hc‚ÇÉ] at hx‚ÇÇ ‚ä¢
            exact hf_inj hx‚ÇÅ hx‚ÇÇ hf'.symm
          ¬∑ rw [if_neg hc‚ÇÇ, if_neg hc‚ÇÉ, ‚Üê ht‚ÇÇ] at hf'
            have := hf_inj ht‚ÇÅ hx‚ÇÇ hf'
            exact absurd this.symm hc‚ÇÉ
      ¬∑ by_cases hc‚ÇÇ : x‚ÇÇ = p
        ¬∑ rw [hc‚ÇÇ] at hf'
          simp only [ite_self, ite_true] at hf'
          by_cases hc‚ÇÉ : x‚ÇÅ = t
          ¬∑ rw [if_neg hc‚ÇÅ, if_pos hc‚ÇÉ, ‚Üê ht‚ÇÇ] at hf'
            rw [hc‚ÇÉ] at hx‚ÇÅ ‚ä¢
            rw [hc‚ÇÇ] at hx‚ÇÇ ‚ä¢
            have := hf_inj hx‚ÇÇ hx‚ÇÅ hf'
            exact this.symm
          ¬∑ rw [if_neg hc‚ÇÅ, if_neg hc‚ÇÉ, ‚Üê ht‚ÇÇ] at hf'
            have := hf_inj hx‚ÇÅ ht‚ÇÅ hf'
            exact absurd this hc‚ÇÉ
        ¬∑ dsimp only at hf'
          rw [if_neg hc‚ÇÅ, if_neg hc‚ÇÇ] at hf'
          by_cases hc‚ÇÉ : x‚ÇÅ = t
          ¬∑ by_cases hc‚ÇÑ : x‚ÇÇ = t
            ¬∑ rw [hc‚ÇÉ, hc‚ÇÑ]
            ¬∑ rw [if_pos hc‚ÇÉ, if_neg hc‚ÇÑ] at hf'
              have := hf_inj hp‚ÇÅ hx‚ÇÇ hf'
              exact absurd this.symm hc‚ÇÇ
          ¬∑ by_cases hc‚ÇÑ : x‚ÇÇ = t
            ¬∑ rw [if_neg hc‚ÇÉ, if_pos hc‚ÇÑ] at hf'
              have := hf_inj hx‚ÇÅ hp‚ÇÅ hf'
              exact absurd this hc‚ÇÅ
            ¬∑ rw [if_neg hc‚ÇÉ, if_neg hc‚ÇÑ] at hf'
              exact hf_inj hx‚ÇÅ hx‚ÇÇ hf'

    let M' := M \ {p}
    have hM' : M' ‚äÇ Set.Iio n := by
      by_cases hc : p = n
      ¬∑ suffices Set.Iio (n + 1) \ {n} = Set.Iio n by
          have h‚ÇÅ := Set.diff_ssubset_diff_left hM hp‚ÇÅ
          conv at h‚ÇÅ => right; rw [hc]
          rwa [‚Üê this]
        ext x
        apply Iff.intro
        ¬∑ intro hx‚ÇÅ
          refine Or.elim (Nat.lt_or_eq_of_lt hx‚ÇÅ.left) (by simp) ?_
          intro hx‚ÇÇ
          rw [hx‚ÇÇ] at hx‚ÇÅ
          simp at hx‚ÇÅ
        ¬∑ intro hx‚ÇÅ
          exact ‚ü®Nat.lt_trans hx‚ÇÅ (by simp), Nat.ne_of_lt hx‚ÇÅ‚ü©

      have hp_lt_n : p < n := by
        have := subset_of_ssubset hM
        have hp' : p < n + 1 := this hp‚ÇÅ
        exact Or.elim (Nat.lt_or_eq_of_lt hp') id (absurd ¬∑ hc)

      rw [Set.ssubset_def]
      apply And.intro
      ¬∑ show ‚àÄ x, x ‚àà M' ‚Üí x < n
        intro x hx
        simp only [Set.mem_diff, Set.mem_singleton_iff] at hx
        calc x
          _ ‚â§ p := hp‚ÇÇ x hx.left
          _ < n := hp_lt_n
      ¬∑ show ¬¨ ‚àÄ x, x < n ‚Üí x ‚àà M'
        by_contra np
        have := np p hp_lt_n
        simp at this

    -- Consider `g = f' - {‚ü®p, n‚ü©}`. This restriction will allow us to use
    -- the induction hypothesis to prove `g` isn't surjective.
    have ng_surj : ¬¨ Set.SurjOn g M' (Set.Iio n) := by
      refine ih _ hM' g ‚ü®?_, ?_‚ü©
      ¬∑ -- `Set.MapsTo g M' (Set.Iio n)`
        intro x hx
        have hx‚ÇÅ : x ‚àà M := Set.mem_of_mem_diff hx
        apply Or.elim (Nat.lt_or_eq_of_lt $ hg_maps hx‚ÇÅ)
        ¬∑ exact id
        ¬∑ intro hx‚ÇÇ
          rw [‚Üê show g p = n by simp] at hx‚ÇÇ
          exact absurd (hg_inj hx‚ÇÅ hp‚ÇÅ hx‚ÇÇ) hx.right
      ¬∑ -- `Set.InjOn g M'`
        intro x‚ÇÅ hx‚ÇÅ x‚ÇÇ hx‚ÇÇ hg
        have hx‚ÇÅ' : x‚ÇÅ ‚àà M := (Set.diff_subset M {p}) hx‚ÇÅ
        have hx‚ÇÇ' : x‚ÇÇ ‚àà M := (Set.diff_subset M {p}) hx‚ÇÇ
        exact hg_inj hx‚ÇÅ' hx‚ÇÇ' hg

    -- We have shown `g` isn't surjective. This is another way of saying that.
    have ‚ü®a, ha‚ÇÅ, ha‚ÇÇ‚ü© : ‚àÉ a, a < n ‚àß a ‚àâ g '' M' := by
      unfold Set.SurjOn at ng_surj
      rw [Set.subset_def] at ng_surj
      simp only [
        Set.mem_Iio,
        Set.mem_image,
        not_forall,
        not_exists,
        not_and,
        exists_prop
      ] at ng_surj 
      unfold Set.image
      simp only [Set.mem_Iio, Set.mem_setOf_eq, not_exists, not_and]
      exact ng_surj

    -- If `g` isn't surjective then neither is `f`.
    refine absurd (hf_surj $ calc a
      _ < n := ha‚ÇÅ
      _ < n + 1 := by simp) (show ‚Üëa ‚àâ f '' M from ?_)

    suffices g '' M = f '' M by
      rw [‚Üê this]
      show a ‚àâ g '' M
      unfold Set.image at ha‚ÇÇ ‚ä¢
      simp only [Set.mem_Iio, Set.mem_setOf_eq, not_exists, not_and] at ha‚ÇÇ ‚ä¢
      intro x hx
      by_cases hxp : x = p
      ¬∑ rw [if_pos hxp]
        exact (Nat.ne_of_lt ha‚ÇÅ).symm
      ¬∑ refine ha‚ÇÇ x ?_
        exact Set.mem_diff_of_mem hx hxp

    ext x
    simp only [Set.mem_image, Set.mem_Iio]
    apply Iff.intro
    ¬∑ intro ‚ü®y, hy‚ÇÅ, hy‚ÇÇ‚ü©
      by_cases hc‚ÇÅ : y = p
      ¬∑ rw [if_pos hc‚ÇÅ] at hy‚ÇÇ
        rw [hy‚ÇÇ] at ht‚ÇÇ
        exact ‚ü®t, ht‚ÇÅ, ht‚ÇÇ‚ü©
      ¬∑ rw [if_neg hc‚ÇÅ] at hy‚ÇÇ
        by_cases hc‚ÇÇ : y = t
        ¬∑ rw [if_pos hc‚ÇÇ] at hy‚ÇÇ
          exact ‚ü®p, hp‚ÇÅ, hy‚ÇÇ‚ü©
        ¬∑ rw [if_neg hc‚ÇÇ] at hy‚ÇÇ
          exact ‚ü®y, hy‚ÇÅ, hy‚ÇÇ‚ü©
    ¬∑ intro ‚ü®y, hy‚ÇÅ, hy‚ÇÇ‚ü©
      by_cases hc‚ÇÅ : y = p
      ¬∑ refine ‚ü®t, ht‚ÇÅ, ?_‚ü©
        by_cases hc‚ÇÇ : y = t
        ¬∑ rw [hc‚ÇÇ, ht‚ÇÇ] at hy‚ÇÇ
          rw [‚Üê hc‚ÇÅ, ‚Üê hc‚ÇÇ]
          simp only [ite_self, ite_true]
          exact hy‚ÇÇ
        ¬∑ rw [hc‚ÇÅ, ‚Üê Ne.def] at hc‚ÇÇ
          rwa [if_neg hc‚ÇÇ.symm, if_pos rfl, ‚Üê hc‚ÇÅ]
      ¬∑ by_cases hc‚ÇÇ : y = t
        ¬∑ refine ‚ü®p, hp‚ÇÅ, ?_‚ü©
          simp only [ite_self, ite_true]
          rwa [hc‚ÇÇ, ht‚ÇÇ] at hy‚ÇÇ
        ¬∑ refine ‚ü®y, hy‚ÇÅ, ?_‚ü©
          rwa [if_neg hc‚ÇÅ, if_neg hc‚ÇÇ]

/--
No natural number is equinumerous to a proper subset of itself.
-/
theorem pigeonhole_principle {n : ‚Ñï}
  : ‚àÄ {M}, M ‚äÇ Set.Iio n ‚Üí M ‚ââ Set.Iio n := by
  intro M hM nM
  have ‚ü®f, hf‚ü© := nM
  have := pigeonhole_principle_aux n M hM f ‚ü®hf.left, hf.right.left‚ü©
  exact absurd hf.right.right this

/-- #### Corollary 6C

No finite set is equinumerous to a proper subset of itself.
-/
theorem corollary_6c [DecidableEq Œ±] [Nonempty Œ±]
  {S S' : Set Œ±} (hS : Set.Finite S) (h : S' ‚äÇ S)
  : S ‚ââ S' := by
  let T := S \ S'
  have hT : S = S' ‚à™ (S \ S') := by
    simp only [Set.union_diff_self]
    exact (Set.left_subset_union_eq_self (subset_of_ssubset h)).symm

  -- `hF : S' ‚à™ T ‚âà S`.
  -- `hG :      S ‚âà n`.
  -- `hH : S' ‚à™ T ‚âà n`.
  have hF := Set.equinumerous_refl S
  conv at hF => arg 1; rw [hT]
  have ‚ü®n, hG‚ü© := Set.finite_iff_equinumerous_nat.mp hS
  have ‚ü®H, hH‚ü© := Set.equinumerous_trans hF hG

  -- Restrict `H` to `S'` to yield a bijection between `S'` and a proper subset
  -- of `n`.
  let R := (Set.Iio n) \ (H '' T)
  have hR : Set.BijOn H S' R := by
    refine ‚ü®?_, ?_, ?_‚ü©
    ¬∑ -- `Set.MapsTo H S' R`
      intro x hx
      refine ‚ü®hH.left $ Set.mem_union_left T hx, ?_‚ü©
      unfold Set.image
      by_contra nx
      simp only [Finset.mem_coe, Set.mem_setOf_eq] at nx

      have ‚ü®a, ha‚ÇÅ, ha‚ÇÇ‚ü© := nx
      have hc‚ÇÅ : a ‚àà S' ‚à™ T := Set.mem_union_right S' ha‚ÇÅ
      have hc‚ÇÇ : x ‚àà S' ‚à™ T := Set.mem_union_left T hx
      rw [hH.right.left hc‚ÇÅ hc‚ÇÇ ha‚ÇÇ] at ha‚ÇÅ

      have hx‚ÇÅ : {x} ‚äÜ S' := Set.singleton_subset_iff.mpr hx
      have hx‚ÇÇ : {x} ‚äÜ T := Set.singleton_subset_iff.mpr ha‚ÇÅ
      have hx‚ÇÉ := Set.disjoint_sdiff_right hx‚ÇÅ hx‚ÇÇ
      simp only [
        Set.bot_eq_empty,
        Set.le_eq_subset,
        Set.singleton_subset_iff,
        Set.mem_empty_iff_false
      ] at hx‚ÇÉ 
    ¬∑ -- `Set.InjOn H S'`
      intro x‚ÇÅ hx‚ÇÅ x‚ÇÇ hx‚ÇÇ h
      have hc‚ÇÅ : x‚ÇÅ ‚àà S' ‚à™ T := Set.mem_union_left T hx‚ÇÅ
      have hc‚ÇÇ : x‚ÇÇ ‚àà S' ‚à™ T := Set.mem_union_left T hx‚ÇÇ
      exact hH.right.left hc‚ÇÅ hc‚ÇÇ h
    ¬∑ -- `Set.SurjOn H S' R`
      show ‚àÄ r, r ‚àà R ‚Üí r ‚àà H '' S'
      intro r hr
      unfold Set.image
      simp only [Set.mem_setOf_eq]
      dsimp only at hr
      have := hH.right.right hr.left
      simp only [Set.mem_image, Set.mem_union] at this
      have ‚ü®x, hx‚ü© := this
      apply Or.elim hx.left
      ¬∑ intro hx'
        exact ‚ü®x, hx', hx.right‚ü©
      ¬∑ intro hx'
        refine absurd ?_ hr.right
        rw [‚Üê hx.right]
        simp only [Set.mem_image, Finset.mem_coe]
        exact ‚ü®x, hx', rfl‚ü©

  intro hf
  have hf‚ÇÅ : S ‚âà R := Set.equinumerous_trans hf ‚ü®H, hR‚ü©
  have hf‚ÇÇ : R ‚âà Set.Iio n := by
    have ‚ü®k, hk‚ü© := Set.equinumerous_symm hf‚ÇÅ
    exact Set.equinumerous_trans ‚ü®k, hk‚ü© hG
    
  refine absurd hf‚ÇÇ (pigeonhole_principle ?_)
  show R ‚äÇ Set.Iio n
  apply And.intro
  ¬∑ show ‚àÄ r, r ‚àà R ‚Üí r ‚àà Set.Iio n
    intro _ hr
    exact hr.left
  ¬∑ show ¬¨ ‚àÄ r, r ‚àà Set.Iio n ‚Üí r ‚àà R
    intro nr
    have ‚ü®t, ht‚ÇÅ‚ü© : Set.Nonempty T := Set.diff_ssubset_nonempty h
    have ht‚ÇÇ : H t ‚àà Set.Iio n := hH.left (Set.mem_union_right S' ht‚ÇÅ)
    have ht‚ÇÉ : H t ‚àà R := nr (H t) ht‚ÇÇ
    exact absurd ‚ü®t, ht‚ÇÅ, rfl‚ü© ht‚ÇÉ.right

/-- #### Corollary 6D (a)

Any set equinumerous to a proper subset of itself is infinite.
-/
theorem corollary_6d_a [DecidableEq Œ±] [Nonempty Œ±]
  {S S' : Set Œ±} (hS : S' ‚äÇ S) (hf : S ‚âà S')
  : Set.Infinite S := by
  by_contra nS
  simp only [Set.not_infinite] at nS
  exact absurd hf (corollary_6c nS hS)

/-- #### Corollary 6D (b)

The set `œâ` is infinite.
-/
theorem corollary_6d_b
  : Set.Infinite (@Set.univ ‚Ñï) := by
  let S : Set ‚Ñï := { 2 * n | n ‚àà @Set.univ ‚Ñï }
  let f x := 2 * x
  suffices Set.BijOn f (@Set.univ ‚Ñï) S by
    refine corollary_6d_a ?_ ‚ü®f, this‚ü©
    rw [Set.ssubset_def]
    apply And.intro
    ¬∑ simp
    ¬∑ show ¬¨ ‚àÄ x, x ‚àà Set.univ ‚Üí x ‚àà S
      simp only [
        Set.mem_univ,
        true_and,
        Set.mem_setOf_eq,
        forall_true_left,
        not_forall,
        not_exists
      ]
      refine ‚ü®1, ?_‚ü©
      intro x nx
      simp only [mul_eq_one, false_and] at nx

  refine ‚ü®by simp, ?_, ?_‚ü©
  ¬∑ -- `Set.InjOn f Set.univ`
    intro n‚ÇÅ _ n‚ÇÇ _ hf
    match @trichotomous ‚Ñï LT.lt _ n‚ÇÅ n‚ÇÇ with
    | Or.inr (Or.inl r) => exact r
    | Or.inl r =>
      have := (Chapter_4.theorem_4n_ii n‚ÇÅ n‚ÇÇ 1).mp r
      conv at this => left; rw [mul_comm]
      conv at this => right; rw [mul_comm]
      exact absurd hf (Nat.ne_of_lt this)
    | Or.inr (Or.inr r) =>
      have := (Chapter_4.theorem_4n_ii n‚ÇÇ n‚ÇÅ 1).mp r
      conv at this => left; rw [mul_comm]
      conv at this => right; rw [mul_comm]
      exact absurd hf.symm (Nat.ne_of_lt this)
  ¬∑ -- `Set.SurjOn f Set.univ S`
    show ‚àÄ x, x ‚àà S ‚Üí x ‚àà f '' Set.univ
    intro x hx
    unfold Set.image
    simp only [Set.mem_univ, true_and, Set.mem_setOf_eq] at hx ‚ä¢
    exact hx

/-- #### Corollary 6E

Any finite set is equinumerous to a unique natural number.
-/
theorem corollary_6e [Nonempty Œ±] (S : Set Œ±) (hS : Set.Finite S)
  : ‚àÉ! n : ‚Ñï, S ‚âà Set.Iio n  := by
  have ‚ü®n, hf‚ü© := Set.finite_iff_equinumerous_nat.mp hS
  refine ‚ü®n, hf, ?_‚ü©
  intro m hg
  match @trichotomous ‚Ñï LT.lt _ m n with
  | Or.inr (Or.inl r) => exact r
  | Or.inl r =>
    have hh := Set.equinumerous_symm hg
    have hk := Set.equinumerous_trans hh hf
    have hmn : Set.Iio m ‚äÇ Set.Iio n := Set.Iio_nat_lt_ssubset r
    exact absurd hk (pigeonhole_principle hmn)
  | Or.inr (Or.inr r) =>
    have hh := Set.equinumerous_symm hf
    have hk := Set.equinumerous_trans hh hg
    have hnm : Set.Iio n ‚äÇ Set.Iio m := Set.Iio_nat_lt_ssubset r
    exact absurd hk (pigeonhole_principle hnm)

/-- #### Lemma 6F

If `C` is a proper subset of a natural number `n`, then `C ‚âà m` for some `m`
less than `n`.
-/
lemma lemma_6f {n : ‚Ñï}
  : ‚àÄ {C}, C ‚äÇ Set.Iio n ‚Üí ‚àÉ m, m < n ‚àß C ‚âà Set.Iio m := by
  induction n with
  | zero =>
    intro C hC
    unfold Set.Iio at hC
    simp only [Nat.zero_eq, not_lt_zero', Set.setOf_false] at hC
    rw [Set.ssubset_empty_iff_false] at hC
    exact False.elim hC
  | succ n ih =>
    have h_subset_equinumerous
      : ‚àÄ S, S ‚äÜ Set.Iio n ‚Üí
          ‚àÉ m, m < n + 1 ‚àß S ‚âà Set.Iio m := by
      intro S hS
      rw [subset_iff_ssubset_or_eq] at hS
      apply Or.elim hS
      ¬∑ -- `S ‚äÇ Set.Iio n`
        intro h
        have ‚ü®m, hm‚ü© := ih h
        exact ‚ü®m, calc m
          _ < n := hm.left
          _ < n + 1 := by simp, hm.right‚ü©
      ¬∑ -- `S = Set.Iio n`
        intro h
        exact ‚ü®n, by simp, Set.eq_imp_equinumerous h‚ü©

    intro C hC
    by_cases hn : n ‚àà C
    ¬∑ -- Since `C` is a proper subset of `n‚Å∫`, the set `n‚Å∫ - C` is nonempty.
      have hC‚ÇÅ : Set.Nonempty (Set.Iio (n + 1) \ C) := by
        rw [Set.ssubset_def] at hC
        have : ¬¨ ‚àÄ x, x ‚àà Set.Iio (n + 1) ‚Üí x ‚àà C := hC.right
        simp only [Set.mem_Iio, not_forall, exists_prop] at this
        exact this
      -- `p` is the least element of `n‚Å∫ - C`.
      have ‚ü®p, hp‚ü© := Chapter_4.well_ordering_nat hC‚ÇÅ

      let C' := (C \ {n}) ‚à™ {p}
      have hC'‚ÇÅ : C' ‚äÜ Set.Iio n := by
        show ‚àÄ x, x ‚àà C' ‚Üí x ‚àà Set.Iio n
        intro x hx
        match @trichotomous ‚Ñï LT.lt _ x n with
        | Or.inl r => exact r
        | Or.inr (Or.inl r) =>
          rw [r] at hx
          apply Or.elim hx
          ¬∑ intro nx
            simp at nx
          ¬∑ intro nx
            simp only [Set.mem_singleton_iff] at nx
            rw [nx] at hn
            exact absurd hn hp.left.right
        | Or.inr (Or.inr r) =>
          apply Or.elim hx
          ¬∑ intro ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©
            have h‚ÇÉ := subset_of_ssubset hC h‚ÇÅ
            simp only [Set.mem_singleton_iff, Set.mem_Iio] at h‚ÇÇ h‚ÇÉ
            exact Or.elim (Nat.lt_or_eq_of_lt h‚ÇÉ) id (absurd ¬∑ h‚ÇÇ)
          ¬∑ intro h
            simp only [Set.mem_singleton_iff] at h
            have := hp.left.left
            rw [‚Üê h] at this
            exact Or.elim (Nat.lt_or_eq_of_lt this)
              id (absurd ¬∑ (Nat.ne_of_lt r).symm)
      have ‚ü®m, hm‚ÇÅ, hm‚ÇÇ‚ü© := h_subset_equinumerous C' hC'‚ÇÅ

      suffices C' ‚âà C from
        ‚ü®m, hm‚ÇÅ, Set.equinumerous_trans (Set.equinumerous_symm this) hm‚ÇÇ‚ü©
      
      -- Proves `f` is a one-to-one correspondence between `C'` and `C`.
      let f x := if x = p then n else x
      refine ‚ü®f, ?_, ?_, ?_‚ü©
      ¬∑ -- `Set.MapsTo f C' C`
        intro x hx
        dsimp only
        by_cases hxp : x = p
        ¬∑ rw [if_pos hxp]
          exact hn
        ¬∑ rw [if_neg hxp]
          apply Or.elim hx
          ¬∑ exact fun x => x.left
          ¬∑ intro hx‚ÇÅ
            simp only [Set.mem_singleton_iff] at hx‚ÇÅ
            exact absurd hx‚ÇÅ hxp
      ¬∑ -- `Set.InjOn f C'`
        intro x‚ÇÅ hx‚ÇÅ x‚ÇÇ hx‚ÇÇ hf
        dsimp only at hf
        by_cases hx‚ÇÅp : x‚ÇÅ = p
        ¬∑ by_cases hx‚ÇÇp : x‚ÇÇ = p
          ¬∑ rw [hx‚ÇÅp, hx‚ÇÇp]
          ¬∑ rw [if_pos hx‚ÇÅp, if_neg hx‚ÇÇp] at hf
            apply Or.elim hx‚ÇÇ
            ¬∑ intro nx
              exact absurd hf.symm nx.right
            ¬∑ intro nx
              simp only [Set.mem_singleton_iff] at nx
              exact absurd nx hx‚ÇÇp
        ¬∑ by_cases hx‚ÇÇp : x‚ÇÇ = p
          ¬∑ rw [if_neg hx‚ÇÅp, if_pos hx‚ÇÇp] at hf
            apply Or.elim hx‚ÇÅ
            ¬∑ intro nx
              exact absurd hf nx.right
            ¬∑ intro nx
              simp only [Set.mem_singleton_iff] at nx
              exact absurd nx hx‚ÇÅp
          ¬∑ rwa [if_neg hx‚ÇÅp, if_neg hx‚ÇÇp] at hf
      ¬∑ -- `Set.SurjOn f C' C`
        show ‚àÄ x, x ‚àà C ‚Üí x ‚àà f '' C'
        intro x hx
        simp only [
          Set.union_singleton,
          Set.mem_diff,
          Set.mem_singleton_iff,
          Set.mem_image,
          Set.mem_insert_iff,
          exists_eq_or_imp,
          ite_true
        ]
        by_cases nx‚ÇÅ : x = n
        ¬∑ left
          exact nx‚ÇÅ.symm
        ¬∑ right
          by_cases nx‚ÇÇ : x = p
          ¬∑ have := hp.left.right
            rw [‚Üê nx‚ÇÇ] at this
            exact absurd hx this
          ¬∑ exact ‚ü®x, ‚ü®hx, nx‚ÇÅ‚ü©, by rwa [if_neg]‚ü©

    ¬∑ refine h_subset_equinumerous C ?_
      show ‚àÄ x, x ‚àà C ‚Üí x ‚àà Set.Iio n
      intro x hx
      unfold Set.Iio
      apply Or.elim (Nat.lt_or_eq_of_lt (subset_of_ssubset hC hx))
      ¬∑ exact id
      ¬∑ intro hx‚ÇÅ
        rw [hx‚ÇÅ] at hx
        exact absurd hx hn

/-- #### Corollary 6G

Any subset of a finite set is finite.
-/
theorem corollary_6g {S S' : Set Œ±} (hS : Set.Finite S) (hS' : S' ‚äÜ S)
  : Set.Finite S' := by
  rw [subset_iff_ssubset_or_eq] at hS'
  apply Or.elim hS'
  ¬∑ intro h
    rw [Set.finite_iff_equinumerous_nat] at hS
    have ‚ü®n, F, hF‚ü© := hS

    -- Mirrors logic found in `corollary_6c`.
    let T := S \ S'
    let R := (Set.Iio n) \ (F '' T)
    have hR : R ‚äÇ Set.Iio n := by
      rw [Set.ssubset_def]
      apply And.intro
      ¬∑ show ‚àÄ x, x ‚àà R ‚Üí x ‚àà Set.Iio n
        intro _ hx
        exact hx.left
      ¬∑ show ¬¨ ‚àÄ x, x ‚àà Set.Iio n ‚Üí x ‚àà R
        intro nr
        have ‚ü®t, ht‚ÇÅ‚ü© : Set.Nonempty T := Set.diff_ssubset_nonempty h
        have ht‚ÇÇ : F t ‚àà Set.Iio n := hF.left ht‚ÇÅ.left
        have ht‚ÇÉ : F t ‚àà R := nr (F t) ht‚ÇÇ
        exact absurd ‚ü®t, ht‚ÇÅ, rfl‚ü© ht‚ÇÉ.right

    suffices Set.BijOn F S' R by
      have ‚ü®m, hm‚ü© := lemma_6f hR
      have := Set.equinumerous_trans ‚ü®F, this‚ü© hm.right
      exact Set.finite_iff_equinumerous_nat.mpr ‚ü®m, this‚ü©
    refine ‚ü®?_, ?_, ?_‚ü©
    ¬∑ -- `Set.MapsTo f S' R`
      intro x hx
      dsimp only
      simp only [Set.mem_diff, Set.mem_Iio, Set.mem_image, not_exists, not_and]
      apply And.intro
      ¬∑ exact hF.left (subset_of_ssubset h hx)
      ¬∑ intro y hy
        by_contra nf
        have := hF.right.left (subset_of_ssubset h hx) hy.left nf.symm
        rw [this] at hx
        exact absurd hx hy.right
    ¬∑ -- `Set.InjOn f S'`
      intro x‚ÇÅ hx‚ÇÅ x‚ÇÇ hx‚ÇÇ hf
      have h‚ÇÅ : x‚ÇÅ ‚àà S := subset_of_ssubset h hx‚ÇÅ
      have h‚ÇÇ : x‚ÇÇ ‚àà S := subset_of_ssubset h hx‚ÇÇ
      exact hF.right.left h‚ÇÅ h‚ÇÇ hf
    ¬∑ -- `Set.SurjOn f S' R`
      show ‚àÄ x, x ‚àà R ‚Üí x ‚àà F '' S'
      intro x hx

      have h‚ÇÅ := hF.right.right
      unfold Set.SurjOn at h‚ÇÅ
      rw [Set.subset_def] at h‚ÇÅ
      have ‚ü®y, hy‚ü© := h‚ÇÅ x hx.left

      refine ‚ü®y, ?_, hy.right‚ü©
      rw [‚Üê hy.right] at hx
      simp only [Set.mem_image, Set.mem_diff, not_exists, not_and] at hx
      by_contra ny
      exact (hx.right y ‚ü®hy.left, ny‚ü©) rfl

  ¬∑ intro h
    rwa [h]

/-- #### Subset Size

Let `A` be a finite set and `B ‚äÇ A`. Then there exist natural numbers `m, n ‚àà œâ`
such that `B ‚âà m`, `A ‚âà n`, and `m ‚â§ n`.
-/
lemma subset_size [DecidableEq Œ±] [Nonempty Œ±] {A B : Set Œ±}
  (hBA : B ‚äÜ A) (hA : Set.Finite A)
  : ‚àÉ m n : ‚Ñï, B ‚âà Set.Iio m ‚àß A ‚âà Set.Iio n ‚àß m ‚â§ n := by
  have ‚ü®n, hn‚ü© := Set.finite_iff_equinumerous_nat.mp hA
  have ‚ü®m, hm‚ü© := Set.finite_iff_equinumerous_nat.mp (corollary_6g hA hBA)
  refine ‚ü®m, n, hm, hn, ?_‚ü©

  suffices ¬¨ m > n by
    match @trichotomous ‚Ñï LT.lt _ m n with
    | Or.inr (Or.inl hr) =>  -- m = n
      rw [hr]
    | Or.inr (Or.inr hr) =>  -- m > n
      exact absurd hr this
    | Or.inl hr          =>  -- m < n
      exact Nat.le_of_lt hr

  by_contra nr
  have ‚ü®f, hf‚ü© := Set.equinumerous_symm hm
  have ‚ü®g, hg‚ü© := hn

  let h x := f (g x)
  have hh : Set.BijOn h A (h '' A) := by
    refine ‚ü®?_, ?_, Eq.subset rfl‚ü©
    ¬∑ -- `Set.MapsTo h A (ran h)`
      intro x hx
      simp only [Set.mem_image]
      exact ‚ü®x, hx, rfl‚ü©
    ¬∑ -- `Set.InjOn h A`
      refine Set.InjOn.comp hf.right.left hg.right.left ?_
      intro x hx
      exact Nat.lt_trans (hg.left hx) nr

  have : h '' A ‚äÇ A := by
    rw [Set.ssubset_def]
    apply And.intro
    ¬∑ show ‚àÄ x, x ‚àà h '' A ‚Üí x ‚àà A
      intro x hx
      have ‚ü®y, hy‚ÇÅ, hy‚ÇÇ‚ü© := hx
      have h‚ÇÅ : g y ‚àà Set.Iio n := hg.left hy‚ÇÅ
      have h‚ÇÇ : f (g y) ‚àà B := hf.left (Nat.lt_trans h‚ÇÅ nr)
      have h‚ÇÉ : x ‚àà B := by rwa [‚Üê hy‚ÇÇ]
      exact hBA h‚ÇÉ
    ¬∑ rw [Set.subset_def]
      simp only [Set.mem_image, not_forall, not_exists, not_and, exists_prop]
      refine ‚ü®f n, hBA (hf.left nr), ?_‚ü©
      intro x hx
      by_contra nh
      have h‚ÇÅ : g x < n := hg.left hx
      have h‚ÇÇ : g x ‚àà Set.Iio m := Nat.lt_trans h‚ÇÅ nr
      rw [hf.right.left h‚ÇÇ nr nh] at h‚ÇÅ
      simp at h‚ÇÅ
  exact absurd ‚ü®h, hh‚ü© (corollary_6c hA this)

/-- #### Finite Domain and Range Size

Let `A` and `B` be finite sets and `f : A ‚Üí B` be a function. Then there exist
natural numbers `m, n ‚àà œâ` such that `dom f ‚âà m`, `ran f ‚âà n`, and `m ‚â• n`.
-/
theorem finite_dom_ran_size [Nonempty Œ±] {A B : Set Œ±}
  (hA : Set.Finite A) (hB : Set.Finite B) (hf : Set.MapsTo f A B)
  : ‚àÉ m n : ‚Ñï, A ‚âà Set.Iio m ‚àß f '' A ‚âà Set.Iio n ‚àß m ‚â• n := by
  have ‚ü®m, hm‚ü© := Set.finite_iff_equinumerous_nat.mp hA
  have ‚ü®p, hp‚ü© := Set.finite_iff_equinumerous_nat.mp hB
  have ‚ü®g, hg‚ü© := Set.equinumerous_symm hm

  let A_y y := { x ‚àà Set.Iio m | f (g x) = y }
  have hA‚ÇÅ : ‚àÄ y ‚àà B, A_y y ‚âà f ‚Åª¬π' {y} := by
    sorry
  have hA‚ÇÇ : ‚àÄ y ‚àà B, Set.Nonempty (A_y y) := by
    sorry
  have hA‚ÇÉ : ‚àÄ y ‚àà B, ‚àÉ q : ‚Ñï, ‚àÄ p ‚àà A_y y, q ‚â§ p := by
    sorry

  let C := { q | ‚àÉ y ‚àà B, ‚àÄ p ‚àà A_y y, q ‚â§ p }
  let h x := f (g x)
  have hh : C ‚âà f '' A := by
    sorry

  sorry

/-- #### Set Difference Size

Let `A ‚âà m` for some natural number `m` and `B ‚äÜ A`. Then there exists some
`n ‚àà œâ` such that `B ‚âà n` and `A - B ‚âà m - n`.
-/
lemma sdiff_size_aux [DecidableEq Œ±] [Nonempty Œ±]
  : ‚àÄ A : Set Œ±, A ‚âà Set.Iio m ‚Üí
      ‚àÄ B, B ‚äÜ A ‚Üí
        ‚àÉ n : ‚Ñï, B ‚âà Set.Iio n ‚àß A \ B ‚âà (Set.Iio m) \ (Set.Iio n) := by
  induction m with
  | zero =>
    intro A hA B hB
    refine ‚ü®0, ?_‚ü©
    simp only [
      Nat.zero_eq,
      sdiff_self,
      Set.bot_eq_empty,
      Set.equinumerous_zero_iff_emptyset
    ] at hA ‚ä¢
    have hB' : B = ‚àÖ := Set.subset_eq_empty hB hA
    have : A \ B = ‚àÖ := by
      rw [hB']
      simp only [Set.diff_empty]
      exact hA
    rw [this]
    refine ‚ü®hB', Set.equinumerous_emptyset_emptyset‚ü©

  | succ m ih =>
    intro A ‚ü®f, hf‚ü© B hB

    -- Since `f` is one-to-one and onto, there exists a unique value `a ‚àà A`
    -- such that `f(a) = m`.
    have hfa := hf.right.right
    unfold Set.SurjOn at hfa
    have ‚ü®a, ha‚ÇÅ, ha‚ÇÇ‚ü© := (Set.subset_def ‚ñ∏ hfa) m (by simp)

    -- `f` is a one-to-one correspondence between `A - {a}` and `m`.
    have hBA : B \ {a} ‚äÜ A \ {a} := Set.diff_subset_diff_left hB
    have hfBA : Set.BijOn f (A \ {a}) (Set.Iio m) := by
      refine ‚ü®?_, ?_, ?_‚ü©
      ¬∑ intro x hx
        have := hf.left hx.left
        simp only [Set.mem_Iio, gt_iff_lt] at this ‚ä¢
        apply Or.elim (Nat.lt_or_eq_of_lt this)
        ¬∑ simp
        ¬∑ intro h
          rw [‚Üê ha‚ÇÇ] at h
          exact absurd (hf.right.left hx.left ha‚ÇÅ h) hx.right
      ¬∑ intro x‚ÇÅ hx‚ÇÅ x‚ÇÇ hx‚ÇÇ h
        exact hf.right.left hx‚ÇÅ.left hx‚ÇÇ.left h
      ¬∑ have := hf.right.right
        unfold Set.SurjOn Set.image at this ‚ä¢
        rw [Set.subset_def] at this ‚ä¢
        simp only [
          Set.mem_Iio,
          Set.mem_diff,
          Set.mem_singleton_iff,
          Set.mem_setOf_eq
        ] at this ‚ä¢
        intro x hx
        have ‚ü®b, hb‚ü© := this x (Nat.lt.step hx)
        refine ‚ü®b, ‚ü®hb.left, ?_‚ü©, hb.right‚ü©
        by_contra nb
        rw [‚Üê nb, hb.right] at ha‚ÇÇ
        exact absurd ha‚ÇÇ (Nat.ne_of_lt hx)

    -- `(A - {a}) - (B - {a}) ‚âà m - n`
    have ‚ü®n, hn‚ÇÅ, hn‚ÇÇ‚ü© := ih (A \ {a}) ‚ü®f, hfBA‚ü© (B \ {a}) hBA
    by_cases hc : a ‚àà B
    ¬∑ refine ‚ü®n.succ, ?_, ?_‚ü©
      ¬∑ -- `B ‚âà Set.Iio n.succ`
        have ‚ü®g, hg‚ü© := hn‚ÇÅ
        let g' x := if x = a then n else g x
        refine ‚ü®g', ‚ü®?_, ?_, ?_‚ü©‚ü©
        ¬∑ -- `Set.MapsTo g' B (Set.Iio n.succ)`
          intro x hx
          dsimp only
          by_cases hx' : x = a
          ¬∑ rw [if_pos hx']
            simp
          ¬∑ rw [if_neg hx']
            calc g x
              _ < n := hg.left ‚ü®hx, hx'‚ü©
              _ < n + 1 := by simp
        ¬∑ -- `Set.InjOn g' B`
          intro x‚ÇÅ hx‚ÇÅ x‚ÇÇ hx‚ÇÇ h
          dsimp only at h
          by_cases hc‚ÇÅ : x‚ÇÅ = a <;> by_cases hc‚ÇÇ : x‚ÇÇ = a
          ¬∑ rw [hc‚ÇÅ, hc‚ÇÇ]
          ¬∑ rw [if_pos hc‚ÇÅ, if_neg hc‚ÇÇ] at h
            exact absurd h.symm (Nat.ne_of_lt $ hg.left ‚ü®hx‚ÇÇ, hc‚ÇÇ‚ü©)
          ¬∑ rw [if_neg hc‚ÇÅ, if_pos hc‚ÇÇ] at h
            exact absurd h (Nat.ne_of_lt $ hg.left ‚ü®hx‚ÇÅ, hc‚ÇÅ‚ü©)
          ¬∑ rw [if_neg hc‚ÇÅ, if_neg hc‚ÇÇ] at h
            exact hg.right.left ‚ü®hx‚ÇÅ, hc‚ÇÅ‚ü© ‚ü®hx‚ÇÇ, hc‚ÇÇ‚ü© h
        ¬∑ -- `Set.SurjOn g' B (Set.Iio n.succ)`
          have := hg.right.right
          unfold Set.SurjOn Set.image at this ‚ä¢
          rw [Set.subset_def] at this ‚ä¢
          simp only [Set.mem_Iio, Set.mem_setOf_eq] at this ‚ä¢
          intro x hx
          by_cases hc‚ÇÅ : x = n
          ¬∑ refine ‚ü®a, hc, ?_‚ü©
            simp only [ite_true]
            exact hc‚ÇÅ.symm
          ¬∑ apply Or.elim (Nat.lt_or_eq_of_lt hx)
            ¬∑ intro hx‚ÇÅ
              have ‚ü®b, ‚ü®hb‚ÇÅ, hb‚ÇÇ‚ü©, hb‚ÇÉ‚ü© := this x hx‚ÇÅ
              refine ‚ü®b, hb‚ÇÅ, ?_‚ü©
              simp only [Set.mem_singleton_iff] at hb‚ÇÇ
              rwa [if_neg hb‚ÇÇ]
            ¬∑ intro hx‚ÇÅ
              exact absurd hx‚ÇÅ hc‚ÇÅ
      ¬∑ have hA‚ÇÅ : (A \ {a}) \ (B \ {a}) = (A \ B) \ {a} :=
          Set.diff_mem_diff_mem_eq_diff_diff_mem
        have hA‚ÇÇ : (A \ B) \ {a} = A \ B := by
          refine Set.not_mem_diff_eq_self ?_
          by_contra na
          exact absurd hc na.right
        rw [hA‚ÇÅ, hA‚ÇÇ] at hn‚ÇÇ
        suffices (Set.Iio m) \ (Set.Iio n) ‚âà (Set.Iio m.succ) \ (Set.Iio n.succ)
          from Set.equinumerous_trans hn‚ÇÇ this
        -- `m - n ‚âà m‚Å∫ - n‚Å∫`
        refine ‚ü®fun x => x + 1, ?_, ?_, ?_‚ü©
        ¬∑ intro x ‚ü®hx‚ÇÅ, hx‚ÇÇ‚ü©
          simp at hx‚ÇÅ hx‚ÇÇ ‚ä¢
          exact ‚ü®Nat.le_add_of_sub_le hx‚ÇÇ, Nat.add_lt_of_lt_sub hx‚ÇÅ‚ü©
        ¬∑ intro _ _ _ _ h
          simp only [add_left_inj] at h
          exact h
        ¬∑ unfold Set.SurjOn Set.image
          rw [Set.subset_def]
          intro x ‚ü®hx‚ÇÅ, hx‚ÇÇ‚ü©
          simp only [
            Set.Iio_diff_Iio,
            gt_iff_lt,
            not_lt,
            ge_iff_le,
            Set.mem_setOf_eq,
            Set.mem_Iio
          ] at hx‚ÇÅ hx‚ÇÇ ‚ä¢
          have ‚ü®p, hp‚ü© : ‚àÉ p : ‚Ñï, x = p.succ := by
            refine Nat.exists_eq_succ_of_ne_zero ?_
            have := calc 0
              _ < n.succ := by simp
              _ ‚â§ x := hx‚ÇÇ
            exact Nat.pos_iff_ne_zero.mp this
          refine ‚ü®p, ‚ü®?_, ?_‚ü©, hp.symm‚ü©
          ¬∑ rw [hp] at hx‚ÇÇ
            exact Nat.lt_succ.mp hx‚ÇÇ
          ¬∑ rw [hp] at hx‚ÇÅ
            exact Nat.succ_lt_succ_iff.mp hx‚ÇÅ
    ¬∑ refine ‚ü®n, ?_, ?_‚ü©
      ¬∑ suffices B \ {a} = B by rwa [‚Üê this]
        exact Set.not_mem_diff_eq_self hc
      ¬∑ sorry

lemma sdiff_size [DecidableEq Œ±] [Nonempty Œ±] {A B : Set Œ±}
  (hB : B ‚äÜ A) (hA : A ‚âà Set.Iio m)
  : ‚àÉ n : ‚Ñï, B ‚âà Set.Iio n ‚àß A \ B ‚âà (Set.Iio m) \ (Set.Iio n) :=
  sdiff_size_aux A hA B hB

/-- #### Exercise 6.7

Assume that `A` is finite and `f : A ‚Üí A`. Show that `f` is one-to-one **iff**
`ran f = A`.
-/
theorem exercise_6_7 [DecidableEq Œ±] [Nonempty Œ±] {A : Set Œ±} {f : Œ± ‚Üí Œ±}
  (hA‚ÇÅ : Set.Finite A) (hA‚ÇÇ : Set.MapsTo f A A)
  : Set.InjOn f A ‚Üî f '' A = A := by
  apply Iff.intro
  ¬∑ intro hf
    have hf‚ÇÇ : A ‚âà f '' A := by
      refine ‚ü®f, ?_, hf, ?_‚ü©
      ¬∑ -- `Set.MapsTo f A (f '' A)`
        intro x hx
        simp only [Set.mem_image]
        exact ‚ü®x, hx, rfl‚ü©
      ¬∑ -- `Set.SurjOn f A (f '' A)`
        intro _ hx
        exact hx
    have hf‚ÇÉ : f '' A  ‚äÜ A := by
      show ‚àÄ x, x ‚àà f '' A ‚Üí x ‚àà A
      intro x ‚ü®a, ha‚ÇÅ, ha‚ÇÇ‚ü©
      rw [‚Üê ha‚ÇÇ]
      exact hA‚ÇÇ ha‚ÇÅ
    rw [subset_iff_ssubset_or_eq] at hf‚ÇÉ
    exact Or.elim hf‚ÇÉ (fun h => absurd hf‚ÇÇ (corollary_6c hA‚ÇÅ h)) id
  ¬∑ intro hf‚ÇÅ
    by_cases hA‚ÇÉ : A = ‚àÖ
    ¬∑ rw [hA‚ÇÉ]
      simp
    ¬∑ intro x‚ÇÅ hx‚ÇÅ x‚ÇÇ hx‚ÇÇ hf‚ÇÇ
      let y := f x‚ÇÅ
      let B := f ‚Åª¬π' {y}
      have hB‚ÇÅ : x‚ÇÅ ‚àà B := sorry
      have hB‚ÇÇ : x‚ÇÇ ‚àà B := sorry
      have hB‚ÇÉ : B ‚äÜ A := sorry
      have ‚ü®m‚ÇÅ, n‚ÇÅ, hm‚ÇÅ, hn‚ÇÅ, hmn‚ÇÅ‚ü© := subset_size hB‚ÇÉ hA‚ÇÅ
      
      have hf'‚ÇÅ : Set.MapsTo f (A \ B) (A \ {y}) := sorry
      have hf'‚ÇÇ : f '' (A \ B) = A \ {y} := sorry
      have hf'‚ÇÉ : Set.Finite (A \ B) := sorry
      have hf'‚ÇÑ : Set.Finite (A \ {y}) := sorry

      have ‚ü®m‚ÇÇ, n‚ÇÇ, hm‚ÇÇ, hn‚ÇÇ, hmn‚ÇÇ‚ü© := finite_dom_ran_size hf'‚ÇÉ hf'‚ÇÑ hf'‚ÇÅ
      
      have h‚ÇÅ : A \ B ‚âà Set.Iio (n‚ÇÅ - m‚ÇÅ) := sorry
      have h‚ÇÇ : A \ {y} ‚âà Set.Iio (n‚ÇÅ - 1) := sorry
      sorry

/-- #### Exercise 6.8

Prove that the union of two finites sets is finite, without any use of
arithmetic.
-/
theorem exercise_6_8 {A B : Set Œ±} (hA : Set.Finite A) (hB : Set.Finite B)
  : Set.Finite (A ‚à™ B) := by
  sorry

/-- #### Exercise 6.9

Prove that the Cartesian product of two finites sets is finite, without any use
of arithmetic.
-/
theorem exercise_6_9 {A : Set Œ±} {B : Set Œ≤}
  (hA : Set.Finite A) (hB : Set.Finite B)
  : Set.Finite (Set.prod A B) := by
  sorry

end Enderton.Set.Chapter_6
